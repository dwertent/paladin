def libraries = [
    'core/go/build/libs',
    'core/java/build/libs',
    'toolkit/go/build/libs'
]

def domains = [
    'domains/noto/build/libs',
    'domains/pente/build/libs',
    'domains/zeto/build/libs'
]

def registries = [
    'registries/static/build/libs',
    'registries/evm/build/libs',
]

def transports = [
    'transports/grpc/build/libs',
]

def signingmodules = [
    'signingmodules/example/build/libs',
]

def uiClient = [
    'ui/client/dist',
]

def assembleSubprojects = [
    ':core:go',
    ':core:java',
    ':toolkit:go',
    ':domains:pente',
    ':domains:noto',
    ':domains:zeto',
    ':registries:static',
    ':registries:evm',
    ':signingmodules:example',
    ':transports:grpc',
    ':ui:client',
]

ext.helpers = [
    lockResource: { task, lockFile ->
        // If two tasks share an output file, they will not be able to run concurrently.
        // This helper creates an empty "lock" file in the root build directory, which allows
        // tasks to use it as a representation of some shared resource.
        def f = new File(task.project.rootProject.buildDir, lockFile)
        task.outputs.file(f)
        task.doLast { f.createNewFile() }
    },

    dumpLogsOnFailure: { task, dockerTaskPath ->
        // If the given Exec task fails, look up the DockerCompose task specified, and ask it to dump logs.
        task.ignoreExitValue(true)
        task.doLast {
            def execResult = getExecutionResult().get()
            if (execResult.exitValue != 0) {
                def docker = project.tasks.getByPath(dockerTaskPath)
                println "\nTask '${task.path}' failed. Dumping Docker logs from '${dockerTaskPath}'."
                docker.dumpLogs(project.file('build/docker-compose.log'))
            }
            execResult.assertNormalExitValue()
        }
    },
    
    dumpChartInfo: { task, taskName, paladinNamespace="default", besuNamespace="default" ->
        def dumpChartInfoOnFailure = task.project.hasProperty('dumpChartInfoOnFailure') ? task.project.dumpChartInfoOnFailure.toBoolean() : false
        
        if (task instanceof Exec) {
            task.ignoreExitValue = true
            task.doLast {
                def execResult = task.getExecutionResult().get()
                if (execResult.exitValue != 0) {
                    println "Task '${task.name}' failed with exit code ${execResult.exitValue}. Dumping logs..."
                    
                    // Check if log dumping is enabled
                    if (!dumpChartInfoOnFailure) {
                        println "Log dumping is disabled. Set -PdumpChartInfoOnFailure=true to enable."
                    } else {
                        println "Dumping logs and diagnostic information for '${task.name}'"
                        def script = new File(new File(task.project.rootProject.projectDir, "scripts"), "dump-logs.sh")
                        if (!script.exists()) {
                            println "Error: dump-logs.sh script not found!"
                        } else {
                            def command = [script.absolutePath, taskName, paladinNamespace, besuNamespace]
                            def process = command.execute(null, task.project.rootProject.projectDir)
                            // Stream the script's output to the Gradle console
                            process.waitForProcessOutput(System.out, System.err)
                            if (process.exitValue() != 0) {
                                println "Warning: Log dumping script failed with exit code ${process.exitValue()}"
                            }
                        }
                        
                        // Re-throw to fail the build
                        execResult.assertNormalExitValue()
                    }
                }
            }
        } else {
            // For non-Exec tasks, add diagnostic information and failure handling
            task.doLast {
                // Check if log dumping is enabled
                if (dumpChartInfoOnFailure) {
                    // Check if the task failed by looking at its state
                    if (task.state.failure != null) {
                        println "Task '${task.name}' FAILED with exception: ${task.state.failure.message}"
                        println "Collecting failure diagnostic information..."
                        
                        // Collect diagnostic information only on failure
                        println "--- Diagnostic Information for Task '${task.name}' ---"
                        println "Task Type: ${task.class.simpleName}"
                        println "Project: ${task.project.name}"
                        println "Task Path: ${task.path}"
                        println "Task Dependencies: ${task.dependsOn}"
                        println "Task Actions: ${task.actions.size()}"
                        
                        // Try to dump logs if the script exists
                        def script = new File(new File(task.project.rootProject.projectDir, "scripts"), "dump-logs.sh")
                        if (script.exists()) {
                            println "Dumping diagnostic information..."
                            def command = [script.absolutePath, taskName, paladinNamespace, besuNamespace]
                            def process = command.execute(null, task.project.rootProject.projectDir)
                            // Stream the script's output to the Gradle console
                            process.waitForProcessOutput(System.out, System.err)
                            if (process.exitValue() != 0) {
                                println "Warning: Log dumping script failed with exit code ${process.exitValue()}"
                            }
                        } else {
                            println "Note: dump-logs.sh script not found, skipping log collection"
                        }
                        
                        println "--- End Diagnostic Information ---"
                    }
                    // If task succeeded, do nothing (no diagnostics needed)
                }
            }
            
            // Add a simple approach: wrap the task's doLast actions in try-catch
            def originalActions = task.actions.toList()
            task.actions.clear()
            
            // Re-add actions with exception handling
            originalActions.each { action ->
                task.doLast {
                    try {
                        action.execute(task)
                    } catch (Exception e) {
                        // Task failed with an exception - collect diagnostics
                        if (dumpChartInfoOnFailure) {
                            println "Task '${task.name}' FAILED with exception: ${e.message}"
                            println "Collecting failure diagnostic information..."
                            
                            // Collect diagnostic information
                            println "--- Diagnostic Information for Task '${task.name}' ---"
                            println "Task Type: ${task.class.simpleName}"
                            println "Project: ${task.project.name}"
                            println "Task Path: ${task.path}"
                            println "Task Dependencies: ${task.dependsOn}"
                            println "Task Actions: ${task.actions.size()}"
                            println "Exception: ${e.class.simpleName}: ${e.message}"
                            
                            // Try to dump logs if the script exists
                            def script = new File(new File(task.project.rootProject.projectDir, "scripts"), "dump-logs.sh")
                            if (script.exists()) {
                                println "Dumping diagnostic information..."
                                def command = [script.absolutePath, taskName, paladinNamespace, besuNamespace]
                                def process = command.execute(null, task.project.rootProject.projectDir)
                                process.waitForProcessOutput(System.out, System.err)
                                if (process.exitValue() != 0) {
                                    println "Warning: Log dumping script failed with exit code ${process.exitValue()}"
                                }
                            } else {
                                println "Note: dump-logs.sh script not found, skipping log collection"
                            }
                            
                            println "--- End Diagnostic Information ---"
                        }
                        
                        // Re-throw the exception to maintain the original failure behavior
                        throw e
                    }
                }
            }
        }
    }
]

// Enable parallel execution for all projects
allprojects {
    // Optimize Java compilation
    tasks.withType(JavaCompile) {
        options.fork = true
        options.incremental = true
        options.encoding = 'UTF-8'
    }
    
    // Optimize test execution
    tasks.withType(Test) {
        maxParallelForks = Math.max(1, Runtime.runtime.availableProcessors().intdiv(2))
        forkEvery = 0
        testLogging {
            events "passed", "skipped", "failed"
            exceptionFormat = 'full'
        }
    }
}
 

// Optimize task execution order
tasks.withType(Test) {
    // Run tests in parallel where possible
    maxParallelForks = Math.max(1, Runtime.runtime.availableProcessors().intdiv(2))
    
    // Enable test batching for faster execution
    forkEvery = 0
}

// Configure lint tasks to run sequentially to avoid conflicts
allprojects {
    tasks.withType(Exec) {
        if (name == 'lint') {
            // Use a shared lock for all lint tasks to prevent conflicts
            // This ensures golangci-lint tasks don't run in parallel
            helpers.lockResource(it, "lint.lock")
        }
        
        // Optimize integration test tasks to run in parallel
        if (name == 'test' && project.name == 'integration-test') {
            // Allow integration tests to run in parallel with other tasks
            // This prevents them from blocking the entire build
            outputs.cacheIf { true }
            
            // Enable parallel execution for integration tests
            doFirst {
                println "Starting integration tests in parallel mode"
            }
        }
    }
}

task jar {
    enabled = false
}

task installProtocGenGo(type: GoInstall) {
    tool = "google.golang.org/protobuf/cmd/protoc-gen-go"
    toolVersion = "v1.34.2"
}

task installProtocGenGoRPC(type: GoInstall) {
    tool = "google.golang.org/grpc/cmd/protoc-gen-go-grpc"
    toolVersion = "v1.5.1"
}

task installGolangCILint(type: GoInstall) {
    tool = "github.com/golangci/golangci-lint/v2/cmd/golangci-lint"
    toolVersion = "v2.3.1"
}

task installMockery(type: GoInstall) {
    tool = "github.com/vektra/mockery/v3"
    toolVersion = "v3.3.2"
}

// Task to copy all executables and binaries to the root-level build/libs directory
task copyLibraries(type: Copy) {
    def libsDir = file("${buildDir}/libs")

    doFirst {
        libsDir.mkdirs()
    }

    libraries.each { dir ->
        from dir
    }
    into libsDir

    outputs.upToDateWhen { false }

    doLast {
        println "Copied executables to ${libsDir}"
    }
}

task copyDomains(type: Copy) {
    def domainsDir = file("${buildDir}/domains")

    doFirst {
        domainsDir.mkdirs()
    }

    domains.each { dir ->
        from dir
    }
    into domainsDir

    outputs.upToDateWhen { false }

    doLast {
        println "Copied domains to ${domainsDir}"
    }
}

task copyRegistries(type: Copy) {
    def registriesDir = file("${buildDir}/registries")

    doFirst {
        registriesDir.mkdirs()
    }

    registries.each { dir ->
        from dir
    }
    into registriesDir

    outputs.upToDateWhen { false }

    doLast {
        println "Copied registries to ${registriesDir}"
    }
}

task copySigningModules(type: Copy) {
    def signingModulesDir = file("${buildDir}/signingmodules")

    doFirst {
        signingModulesDir.mkdirs()
    }

    signingmodules.each { dir ->
        from dir
    }
    into signingModulesDir

    outputs.upToDateWhen { false }

    doLast {
        println "Copied signing modules to ${signingModulesDir}"
    }
}

task copyTransports(type: Copy) {
    def transportsDir = file("${buildDir}/transports")

    doFirst {
        transportsDir.mkdirs()
    }

    transports.each { dir ->
        from dir
    }
    into transportsDir

    outputs.upToDateWhen { false }

    doLast {
        println "Copied transports to ${transportsDir}"
    }
}

task copyUIFiles(type: Copy) {
    def uiDir = file("${buildDir}/ui")

    doFirst {
        uiDir.mkdirs()
    }

    uiClient.each { dir ->
        from dir
    }
    into uiDir

    outputs.upToDateWhen { false }

    doLast {
        println "Copied UI files to ${uiDir}"
    }
}

task copyZetoZKPFiles(type: Copy) {
    def zetoZkpDir = file("${buildDir}/domains/zeto/zkp")

    doFirst {
        zetoZkpDir.mkdirs()
    }

    // These can be very large binaries, so adding to this list in the base image should be done with care.
    // Likely if the range of circuits grows, a separate docker tag will be required for builds
    // of Paladin that have a wider range of pre-built circuits included.
    from fileTree('domains/zeto/zkp') {
        // anon
        include 'anon.zkey'
        include 'anon-vkey.json'
        include 'anon_js/anon.wasm'
        include 'anon_batch.zkey'
        include 'anon_batch-vkey.json'
        include 'anon_batch_js/anon_batch.wasm'
        // anon_enc
        include 'anon_enc.zkey'
        include 'anon_enc-vkey.json'
        include 'anon_enc_js/anon_enc.wasm'
        include 'anon_enc_batch.zkey'
        include 'anon_enc_batch-vkey.json'
        include 'anon_enc_batch_js/anon_enc_batch.wasm'
        // anon_nullifier
        include 'anon_nullifier_transfer.zkey'
        include 'anon_nullifier_transfer-vkey.json'
        include 'anon_nullifier_transfer_js/anon_nullifier_transfer.wasm'
        include 'anon_nullifier_transfer_batch.zkey'
        include 'anon_nullifier_transfer_batch-vkey.json'
        include 'anon_nullifier_transfer_batch_js/anon_nullifier_transfer_batch.wasm'
        // anon_nullifier_kyc
        include 'anon_nullifier_kyc_transfer.zkey'
        include 'anon_nullifier_kyc_transfer-vkey.json'
        include 'anon_nullifier_kyc_transfer_js/anon_nullifier_kyc_transfer.wasm'
        include 'anon_nullifier_kyc_transfer_batch.zkey'
        include 'anon_nullifier_kyc_transfer_batch-vkey.json'
        include 'anon_nullifier_kyc_transfer_batch_js/anon_nullifier_kyc_transfer_batch.wasm'
        // deposit
        include 'deposit.zkey'
        include 'deposit-vkey.json'
        include 'deposit_js/deposit.wasm'
        // withdraw
        include 'withdraw.zkey'
        include 'withdraw-vkey.json'
        include 'withdraw_js/withdraw.wasm'
        include 'withdraw_batch.zkey'
        include 'withdraw_batch-vkey.json'
        include 'withdraw_batch_js/withdraw_batch.wasm'
        // withdraw_nullifier
        include 'withdraw_nullifier.zkey'
        include 'withdraw_nullifier-vkey.json'
        include 'withdraw_nullifier_js/withdraw_nullifier.wasm'
        include 'withdraw_nullifier_batch.zkey'
        include 'withdraw_nullifier_batch-vkey.json'
        include 'withdraw_nullifier_batch_js/withdraw_nullifier_batch.wasm'
    }
    into zetoZkpDir

    outputs.upToDateWhen { false }

    doLast {
        println "Copied zetoZkp files to ${zetoZkpDir}"
    }
}

// Root assemble task that depends on subproject's assemble tasks
task assemble {
    assembleSubprojects.each { projectPath ->
        dependsOn "${projectPath}:assemble"
    }
}

assemble.finalizedBy(copyLibraries, copyDomains, copyRegistries, copySigningModules, copyTransports, copyUIFiles, copyZetoZKPFiles)

// Enable parallel execution of copy tasks
tasks.withType(Copy) {
    // Allow copy tasks to run in parallel
    outputs.cacheIf { true }
}

defaultTasks 'build'

// Clean task to clean all projects
task clean {
    delete "${buildDir}"
}

task docker(type: Exec) {
    executable 'docker'
    args 'build'
    args '-t', 'paladin:test'
    args '.'
}

task dockerx(type: Exec) {
    executable 'docker'
    args 'buildx', 'build'
    args '--platform', 'linux/amd64,linux/arm64'
    args '-t', 'paladin:test'
    args '.'
}
